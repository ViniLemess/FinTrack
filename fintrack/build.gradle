import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.9.20'
    id 'org.jetbrains.kotlin.jvm' version '1.9.20'
}

group = 'org.fundatec.vinilemess'
version = '1.0.0'
sourceCompatibility = JavaVersion.VERSION_21

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

ext {
    testContainersVersion = "1.20.1"
    kotlinVersion = "1.9.20"
    jwtVersion = "0.12.6"
}

dependencies {
    // Spring
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'

    // kotlin libraries
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.18.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.9.0"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

    // JWT authentication
    implementation "io.jsonwebtoken:jjwt-api:$jwtVersion"
    implementation "io.jsonwebtoken:jjwt-impl:$jwtVersion"
    implementation "io.jsonwebtoken:jjwt-jackson:$jwtVersion"

    testImplementation 'io.mockk:mockk:1.13.12'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.testcontainers:testcontainers:$testContainersVersion"
    testImplementation "org.testcontainers:junit-jupiter:$testContainersVersion"
    testImplementation "org.testcontainers:mongodb:$testContainersVersion"
    testImplementation 'io.rest-assured:rest-assured:5.5.0'
}

tasks.register('endToEndTests', Test) {
    group 'verification'
    include '**/*EndToEndTest.*'
    systemProperty 'spring.profiles.active', 'e2e'
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions.jvmTarget = "21"
}

tasks.named('test') {
    exclude '**/*EndToEndTest.*'
    useJUnitPlatform()
}

